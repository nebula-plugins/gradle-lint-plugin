/*
 * Copyright 2014-2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'nebula.plugin-plugin' version '7.1.6'
    id 'nebula.kotlin' version '1.2.10'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'java-gradle-plugin'
}

description 'Pluggable and configurable linter tool for identifying and reporting on patterns of misuse or deprecations in Gradle scripts'

contacts {
    'jkschneider@gmail.com' {
        moniker 'Jon Schneider'
        github 'jkschneider'
    }
}

configurations {
    plugin.description = 'The compile dependencies for the plugin, excluding the local dependencies to avoid problems with Shadow'
    compile.extendsFrom plugin
}

dependencies {
    compile 'com.netflix.nebula:nebula-gradle-interop:latest.release'
    compile 'org.apache.maven:maven-model-builder:3.+'
    compile 'com.netflix.nebula:nebula-dependency-base-plugin:0.1.0'

    plugin 'org.ow2.asm:asm:5.2'
    plugin('org.codenarc:CodeNarc:0.25.2') {
        transitive = false
    }
    plugin 'com.netflix.nebula:gradle-info-plugin:3.3.5'
    plugin 'commons-lang:commons-lang:2.6'

    plugin ('org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r') {
        transitive = false
    }
    plugin 'org.eclipse.jdt:core:3.1.1'

    plugin 'org.codehaus.gpars:gpars:1.2.1'

    // these two dependencies exist so we can provide a test harness
    // to proprietary rule implementations packed in other jars, but are considered provided
    plugin ('org.spockframework:spock-core:1.0-groovy-2.4') { transitive = false }
    plugin ('junit:junit:4.12')
    plugin ('com.netflix.nebula:nebula-test:latest.release') { transitive = false }

    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'org.ow2.asm:asm-util:5.2'
    testCompile 'joda-time:joda-time:latest.release'
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/gradle-lint-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/gradle-lint-plugin.git'
    description = 'Pluggable and configurable linter tool for identifying and reporting on patterns of misuse or deprecations in Gradle scripts'

    plugins {
        dependencyLock {
            id = 'nebula.lint'
            displayName = 'Nebula Lint plugin'
            description = 'Pluggable and configurable linter tool for identifying and reporting on patterns of misuse or deprecations in Gradle scripts'
            tags = ['nebula', 'lint']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'gradle-lint-plugin'
    }
}

// Relocate jgit dependency not available in Maven Central
// Replaces the main artifact by removing the classifier for the shadow jar, and replacing jar with shadowJar
// Relocated dependencies are removed from the generated pom
shadowJar {
    configurations = [project.configurations.plugin]
    classifier = null
    dependencies {
        include(dependency('org.eclipse.jdt:core'))
        include(dependency('org.eclipse.jgit:org.eclipse.jgit'))
        include(dependency('commons-lang:commons-lang'))
        include(dependency('org.codenarc:CodeNarc'))
    }
    relocate 'org.eclipse.jdt', 'com.netflix.nebula.lint.jdt'
    relocate 'org.eclipse.jgit', 'com.netflix.nebula.lint.jgit'
    relocate 'org.apache.commons.lang', 'com.netflix.nebula.lint.commons.lang'
    relocate 'org.codenarc', 'com.netflix.nebula.lint.org.codenarc'

    // powerassert is packed inside codenarc without relocation for some reason
    relocate 'org.codehaus.groovy.transform.powerassert', 'com.netflix.nebula.lint.org.codehaus.groovy.transform.powerassert'
}

jar.deleteAllActions()
jar.dependsOn shadowJar

publishing {
    publications {
        withType(MavenPublication) {
            pom.withXml {
                asNode()
                    .dependencies
                    .dependency
                    .findAll {
                        // JGit is shaded inside the distribution, and we want the others to act as 'provided'
                        ['core', 'org.eclipse.jgit', 'spock-core', 'nebula-test', 'CodeNarc'].contains(it.artifactId.text())
                    }
                    .each { it.parent().remove(it) }
            }
        }
    }
}

// Kotlin compiles first, stubbing Java dependencies, however it can't stub Groovy and we need to call Groovy code from Kotlin. For details see:
// https://discuss.gradle.org/t/kotlin-groovy-and-java-compilation/14903/10
if (GradleVersion.version(project.gradle.gradleVersion).compareTo(GradleVersion.version('4.4.1')) > 0) {
    compileGroovy.dependsOn = compileGroovy.taskDependencies.mutableValues - 'compileJava'
} else {
    compileGroovy.dependsOn = compileGroovy.taskDependencies.values - 'compileJava'
}
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin
